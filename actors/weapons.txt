Const Int WRF_BOBONLY = WRF_NOFIRE | WRF_NOSWITCH;

ACTOR SG_Muzflash //osjclatchford
{
	Radius 1
	Height 1
	VSpeed 1
	Speed 20
	Scale 0.2
	Alpha 0.6
	Renderstyle "Add"
	Projectile
	-RANDOMIZE
	+BRIGHT
	+DONTSPLASH
	+NOCLIP
	States
	{
	Spawn:
		SPKO CCC 1
		Stop
	}
}

ACTOR CG_Muzflash : SG_Muzflash
{
	States
	{
	Spawn:
		SPKB CCC 1
		Stop
	}
}

ACTOR Q2Weapon : DoomWeapon
{
	Tag "[Insert Weapon Name Here]"
	Inventory.PickupMessage "[Insert Weapon Message Here]"
	+WEAPON.NOALERT
	Weapon.BobStyle InverseSmooth
	Weapon.BobSpeed 2
	Weapon.BobRangeX 0.66
	Weapon.BobRangeY 0.66
	States
	{
	Select:
		TNT1 A 0 A_WeaponReady(WRF_BOBONLY)
		TNT1 A 0 A_Jump(256,"Raise")
	Deselect:
	Raise:
	Ready:
	Fire:
		TNT1 A 0 A_TakeInventory(Q2Weapon)
		Stop
	InstantDeselect:
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
		TNT1 A 1 A_Lower
		Wait
	}
}

ACTOR WeaponIndex : InvBoolean { Inventory.MaxAmount 20 }

ACTOR WeaponFlash : EffectBase
{
	States
	{
	Spawn:
		TNT1 A 2
		Stop
	}
}

ACTOR FistReplacer Replaces Fist {}

ACTOR Q2Blaster : Q2Weapon Replaces Pistol
{
	Weapon.SelectionOrder 900
	+INVENTORY.UNDROPPABLE
	Tag "Blaster"
	Weapon.SlotNumber 1
	Inventory.PickupMessage "Blaster"
	Inventory.Icon "W_BLSTR"
	States
	{
	Spawn:
		PSTL A -1
		Stop
	Raise:
		TNT1 A 0 A_TakeInventory(WeaponIndex)
		TNT1 A 0 A_GiveInventory(WeaponIndex,1)
		BLS1 AB 5
		BLS1 CDE 4
		Goto Ready+1
	Ready:
		TNT1 A 0
		BLS2 AAA 6 A_WeaponReady
		TNT1 A 0 A_Jump(64,1)
		Loop
		BLS2 ABCDEFGHIJKL 6 A_WeaponReady
		BLS2 LLL 6 A_WeaponReady
		TNT1 A 0 A_Jump(64,1)
		Goto Ready+17
		BLS2 MNOPQRSTUV 6 A_WeaponReady
		Goto Ready+1
	Deselect:
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("GetCvarInfo",0)==True,"InstantDeselect")
		BLS2 A 3
		BLS1 JKL 4
		Goto InstantDeselect
	Fire:
		TNT1 A 0 A_JumpIfInventory(PowerSilencer,1,4)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("Weapons/BlasterFire",CHAN_WEAPON)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_PlaySound("Weapons/BlasterFire",CHAN_WEAPON,0.5,0,3)
		TNT1 A 0 A_FireCustomMissile(BlasterProjectile,0,0,8,4)
		TNT1 A 0 A_FireCustomMissile(WeaponFlash,0,0)
		TNT1 A 0 A_GunFlash
		BLS1 FGHI 4
		BLS2 A 2
		Goto Ready
	Flash:
		TNT1 A 0 A_Light2
		TNT1 A 0 A_SetPitch(Pitch-3.0)
		TNT1 AAAAA 1 A_SetPitch(Pitch+0.2)
		TNT1 A 0 A_Light1
		TNT1 AAAAA 1 A_SetPitch(Pitch+0.2)
		TNT1 A 0 A_Light0
		TNT1 AAAAA 1 A_SetPitch(Pitch+0.2)
		Stop
	}
}

ACTOR BlasterProjectile : FastProjectile
{
	Radius 3
	Height 6
	Speed 32
	Damage 4
	MissileHeight 8
	MissileType BlasterTrailSpawner
	DeathSound "Weapons/LaserHit"
	+BLOODSPLATTER
	Decal BlasterScorch
	States
	{
	Spawn:
		NULL A 1 Light(BlasterProjectile) BRIGHT A_PlaySound("Weapons/LaserFly",CHAN_VOICE,0.5,1)
		Loop
	Death:
		TNT1 A 0 A_SpawnItem(BlasterProjectileImpact)
		TNT1 A 3 Light(BlasterProjectile_X1)
		TNT1 A 3 Light(BlasterProjectile_X2)
		TNT1 A 3 Light(BlasterProjectile_X3)
		TNT1 A 3 Light(BlasterProjectile_X4)
		Stop
	XDeath:
		TNT1 A 0
		Stop
	}
}

ACTOR BlasterProjectileImpact : EffectBase
{
	Height 6
	+BRIGHT
	RenderStyle Normal
	Translation "80:111=208:223"
	States
	{
	Spawn:
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx(BlasterImpactParticle,0,0,0,FRandom(0,1),0,FRandom(-0.25,3),Random(1,360),1)
		TNT1 A 0 A_CheckFloor("Floor")
		TNT1 A 0 A_CheckCeiling(1)
		Goto Air
		TNT1 A 0 A_SetScale(-1)
	Floor:
		NULL ABC 3
		NULL D 1 A_FadeOut(0.25)
		Wait
	Air:
		NULL EFG 3
		NULL H 1 A_FadeOut(0.25)
		Wait
	}
}

ACTOR BlasterTrailSpawner : EffectBase
{
	Scale 0.66
	States
	{
	Spawn:
		TNT1 AA 0 A_SpawnItemEx(BlasterTrail,FRandom(0,2),0,FRandom(-2,2),FRandom(0,0.15),0,FRandom(-0.15,0.15),Random(1,360),128|SXF_TRANSFERSCALE)
		Stop
	}
}

ACTOR BlasterTrail : Q2BulletPuffParticle { Translation "80:111=208:223" Scale 0.75 }

ACTOR BlasterImpactParticle : BlasterTrail
{
	Projectile
	-NOINTERACTION -NOGRAVITY
	Gravity 0.125
	States
	{
	Fade:
		"----" A 10
		"----" A 1 A_FadeOut
		Wait
	}
}

ACTOR Q2Shotgun : Q2Weapon Replaces Shotgun
{
	Tag "Shotgun"
	Inventory.PickupMessage "$GOTSHOTGUN"
	Weapon.SelectionOrder 300
	Weapon.AmmoType Q2Shell
	Weapon.AmmoUse 1
	Weapon.SlotNumber 2
	Weapon.AmmoGive 4
	Inventory.Icon "W_SHTGN"
	States
	{
	Spawn:
		TNT1 A 0
		W002 D -1
		Stop
	Raise:
		TNT1 A 0 A_TakeInventory(WeaponIndex)
		TNT1 A 0 A_GiveInventory(WeaponIndex,2)
		W001 A 4
		W001 B 3
		W001 C 4
		W001 D 3
		W001 E 4
		W001 F 3
		W001 G 4
		W001 HT 4 A_WeaponReady
		Goto Ready
	Ready:
		TNT1 A 0 A_Jump(32,2)
		W001 T 5 A_WeaponReady
		Loop
		W001 TUVW 15 A_WeaponReady
		W001 XXX 5 A_WeaponReady
		TNT1 A 0 A_Jump(32,1)
		Goto Ready+6
		W001 "YZ[" 15 A_WeaponReady
		W001 "^^^" 5 A_WeaponReady
		TNT1 A 0 A_Jump(32,1)
		Goto Ready+14
		W001 "]T" 15 A_WeaponReady
		Loop
	Deselect:
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("GetCvarInfo",0)==True,"InstantDeselect")
		W001 T 2
		W002 ABC 4
		Goto InstantDeselect
	Fire:
		TNT1 A 0 A_FireCustomMissile("SG_Muzflash", Random(1, -1), 0, 8, 0, 0, Random(1, -1))
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("GetCvarInfo",0)==True,"DFire")
		TNT1 A 0 A_JumpIfInventory(PowerSilencer,1,4)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("Weapons/ShotgunFire",CHAN_WEAPON)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_PlaySound("Weapons/ShotgunFire",CHAN_WEAPON,0.5,0,3)
		TNT1 A 0 A_FireBullets(6,3,10,4)
		TNT1 A 0 A_FireCustomMissile(WeaponFlash,0,0)
		TNT1 A 0 A_GunFlash
		W001 IJ 3
		W001 K 0 A_JumpIfInventory(PowerSilencer,1,4)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("Weapons/ShotgunPump",0)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_PlaySound("Weapons/ShotgunPump",0,0.5)
		W001 KLMN 4
		W001 OPQRST 3
		W001 T 3
		Goto Ready
	DFire:
		TNT1 A 0 A_JumpIfInventory(PowerSilencer,1,4)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("Weapons/ShotgunFire",CHAN_WEAPON)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_PlaySound("Weapons/ShotgunFire",CHAN_WEAPON,0.5,0,3)
		TNT1 A 0 A_FireBullets(6,3,7,5)
		TNT1 A 0 A_FireCustomMissile(WeaponFlash,0,0)
		TNT1 A 0 A_GunFlash
		W001 IJK 3
		W001 L 0 A_JumpIfInventory(PowerSilencer,1,4)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("Weapons/ShotgunPump",0)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_PlaySound("Weapons/ShotgunPump",0,0.5)
		W001 LMN 4
		W001 OPQRST 3
		W001 T 0
		Goto Ready
	Flash:
		TNT1 A 1 A_Light2
		TNT1 A 0 A_SetPitch(Pitch-3.0)
		TNT1 AAAAA 1 A_SetPitch(Pitch+0.3)
		TNT1 A 0 A_Light1
		TNT1 AAAAA 1 A_SetPitch(Pitch+0.3)
		Goto LightDone
		
	}
}

ACTOR Q2SuperShotgun : Q2Weapon Replaces SuperShotgun
{
	Tag "Super Shotgun"
	Inventory.PickupMessage "Super Shotgun"
	Weapon.SelectionOrder 200
	Weapon.AmmoType Q2Shell
	Weapon.AmmoUse 2
	Weapon.SlotNumber 3
	Weapon.AmmoGive 6
	Inventory.Icon "W_SSG"
	States
	{
	Spawn:
		SGN2 A -1
		Stop
	Raise:
		TNT1 A 0 A_TakeInventory(WeaponIndex)
		TNT1 A 0 A_GiveInventory(WeaponIndex,3)
		SSG0 ABCDEFG 4
		Goto Ready+1
	Deselect:
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("GetCvarInfo",0)==True,"InstantDeselect")
		SSG1 A 2
		SSG0 STUV 3
		Goto InstantDeselect
	Ready:
		TNT1 A 0 A_Jump(6,2)
		SSG1 A 1 A_WeaponReady
		Loop
		SSG1 "ABCDEFGHIJKLMNOPQRSTUVWXYZ[^]" 5 A_WeaponReady
		SSG2 ABCDEFGHIJK 5 A_WeaponReady
		Goto Ready+1
	Fire:
		TNT1 A 0 A_FireCustomMissile("SG_Muzflash", Random(1, -1), 0, 10, 4, 0, Random(1, -1))
		TNT1 A 0 A_FireCustomMissile("SG_Muzflash", Random(1, -1), 0, 5, 4, 0, Random(1, -1))
		TNT1 A 0 A_JumpIfInventory(PowerSilencer,1,4)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("Weapons/SuperShotgunFire",CHAN_WEAPON)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_PlaySound("Weapons/SuperShotgunFire",CHAN_WEAPON,0.5,0,3)
		TNT1 A 0 A_FireCustomMissile(WeaponFlash,0,0)
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("GetCvarInfo",0)==True,"DFire")
		TNT1 A 0 A_FireBullets(18,10,20,4)
		TNT1 A 0 A_GunFlash
		SSG0 HIJKLMNOPQR 4
		Goto Ready+1
	DFire:
		TNT1 A 0 A_FireBullets(10,10,21,5)
		TNT1 A 0 A_GunFlash
		SSG0 HIJ 3
		SSG0 KLMNOPQR 4
		SSG1 A 8
		Goto Ready+1
	Flash:
		TNT1 A 1 A_Light2
		TNT1 A 0 A_SetPitch(Pitch-5.0)
		TNT1 AAAAA 2 A_SetPitch(Pitch+0.5)
		TNT1 A 0 A_Light1
		TNT1 AAAAA 2 A_SetPitch(Pitch+0.5)
		Goto LightDone
	}
}

ACTOR Q2Machinegun : Q2Weapon
{
	Tag "Machinegun"
	Inventory.PickupMessage "Machinegun"
	Weapon.SelectionOrder 100
	Weapon.AmmoType Q2Bullets
	Weapon.AmmoUse 1
	Weapon.SlotNumber 4
	Weapon.AmmoGive 20
	Inventory.Icon "W_MCHNGN"
	States
	{
	Spawn:
		MGUN A -1
		Stop
	Raise:
		TNT1 A 0 A_TakeInventory(WeaponIndex)
		TNT1 A 0 A_GiveInventory(WeaponIndex,4)
		MGN0 ABCD 5
		Goto Ready+1
	Deselect:
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("GetCvarInfo",0)==True,"InstantDeselect")
		MGN0 H 3
		MGN1 STUV 3
		Goto InstantDeselect
	Ready:
		TNT1 A 0 A_Jump(6,2)
		MGN0 H 1 A_WeaponReady
		Loop
		MGN0 "IJKLMNOPQRSTUVWXYZ[^]" 5 A_WeaponReady
		MGN1 ABCDEFGHIJKLMNOPQR 5 A_WeaponReady
		Goto Ready+1
	Fire:
		MGN0 F 4 A_GunFlash
		MGN0 E 3 A_GunFlash
		MGN0 F 0 A_JumpIfInventory(Q2Bullets,1,3)
		TNT1 A 0 A_PlayWeaponSound("Weapons/NoAmmo")
		TNT1 A 0 A_CheckReload
		MGN0 F 0 A_ReFire
		TNT1 A 0 A_GunFlash("ClimbDown",GFF_NOEXTCHANGE)
		Goto Ready
	Flash:
		TNT1 A 0 A_FireCustomMissile("SG_Muzflash", Random(1, -1), 0, 10, 0, 0, Random(1, -1))
		TNT1 A 0 A_JumpIfInventory(PowerSilencer,1,4)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("Weapons/MachineGunFire",CHAN_WEAPON)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_PlaySound("Weapons/MachineGunFire",CHAN_WEAPON,0.5,0,3)
		TNT1 A 0 A_FireBullets(5,2,1,5)
		TNT1 A 0 A_FireCustomMissile(WeaponFlash,0,0)
		TNT1 A 0 A_JumpIfInventory(MachinegunClimbToken,0,"Null")
		TNT1 A 0 A_GiveInventory(MachinegunClimbToken,1)
		TNT1 AAA 1 A_SetPitch(Pitch-0.33)
		Stop
	ClimbDown:
		TNT1 A 0 A_JumpIfInventory(MachinegunClimbToken,1,1)
		Stop
		TNT1 A 0 A_TakeInventory(MachinegunClimbToken,2)
		TNT1 A 1 A_SetPitch(Pitch+1.98)
		Loop
	}
}

ACTOR MachinegunClimbToken : InvBoolean { Inventory.MaxAmount 10 }

ACTOR Q2Chaingun : Q2Weapon
{
	Tag "Chaingun"
	Inventory.PickupMessage "Chaingun"
	Weapon.AmmoType Q2Bullets
	Weapon.SelectionOrder 150
	Weapon.AmmoUse 1
	Weapon.SlotNumber 5
	Weapon.AmmoGive 20
	Inventory.Icon "W_CHGN"
	States
	{
	Spawn:
		MGUN A -1
		Stop
	Raise:
		TNT1 A 0 A_TakeInventory(WeaponIndex)
		TNT1 A 0 A_GiveInventory(WeaponIndex,5)
		CHG0 ABCDE 4
		Goto Ready+1
	Deselect:
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("GetCvarInfo",0)==True,"InstantDeselect")
		TNT1 A 0 A_StopSound(CHAN_5)
		CHG0 FGHI 4
		Goto InstantDeselect
	Ready:
		TNT1 A 0 A_Jump(6,2)
		CHG0 F 1 A_WeaponReady
		Loop
		CHG2 "ABCDEFGHIJKLMNOPQRSTUVWXYZ^[]" 5 A_WeaponReady
		Goto Ready+1
	Fire:
		TNT1 A 0 A_PlaySound("Weapons/ChaingunSpinUp",CHAN_5)
		CHG1 A 4 A_GunFlash
		CHG1 B 3 A_GunFlash
		CHG1 C 4 A_GunFlash
		CHG1 D 3 A_GunFlash
		CHG1 E 4 A_GunFlash
		CHG1 F 3 A_GunFlash
		CHG1 G 4 A_GunFlash
		CHG1 H 3 A_GunFlash
		CHG1 I 4 A_GunFlash
		CHG1 J 0 A_ReFire(1)
		Goto Ready+1
		TNT1 A 0 A_PlaySound("Weapons/ChaingunSpinning",CHAN_5,1,1)
		CHG1 JKLMNOPQ 2 A_GunFlash("Flash2")
	Hold:
		CHG1 "RSTUVWXYZ^" 1 A_GunFlash("Flash3")
		CHG1 R 0 A_JumpIfInventory(Q2Bullets,1,2)
		TNT1 A 0 A_PlayWeaponSound("Weapons/NoAmmo")
		Goto SpinDown
		TNT1 A 0 A_ReFire
		CHG1 "RSTUVWXYZ^RSTUVWXYZ^" 1 A_GunFlash("Flash3")
	SpinDown:
		TNT1 A 0 A_PlaySound("Weapons/ChaingunSpinDown",CHAN_5,0.75)
		CHG1 ABCDEFGHIJ 4 A_WeaponReady
		CHG0 F 0 A_CheckReload
		Goto Ready
	Flash:
		TNT1 A 0 A_FireCustomMissile("CG_Muzflash", Random(1, -1), 0, 8, 0, 0, Random(1, -1))
		TNT1 A 0 A_JumpIfInventory(PowerSilencer,1,4)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("Weapons/MachineGunFire",CHAN_WEAPON)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_PlaySound("Weapons/MachineGunFire",CHAN_WEAPON,0.5,0,3)
		TNT1 A 0 A_JumpIfInventory(Q2Bullets,1,3)
		TNT1 A 0 A_PlayWeaponSound("Weapons/NoAmmo")
		TNT1 A 0 A_Jump(256,3)
		TNT1 A 0 A_FireBullets(5,2,-1,6)
		TNT1 A 0 A_FireCustomMissile(WeaponFlash,0,0)
		TNT1 A 0 A_SetAngle(Angle+FRandom(-1.25,1.25))
		TNT1 A 1 A_SetPitch(Pitch+FRandom(-1.25,1.25))
		Stop
	Flash2:
		TNT1 A 0 A_FireCustomMissile("CG_Muzflash", Random(1, -1), 0, 8, 0, 0, Random(1, -1))
		TNT1 A 0 A_JumpIfInventory(PowerSilencer,1,4)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("Weapons/MachineGunFire",CHAN_WEAPON)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_PlaySound("Weapons/MachineGunFire",CHAN_WEAPON,0.5,0,3)
		TNT1 A 0 A_JumpIfInventory(Q2Bullets,1,3)
		TNT1 A 0 A_PlayWeaponSound("Weapons/NoAmmo")
		TNT1 A 0 A_Jump(256,3)
		TNT1 A 0 A_FireBullets(5,5,-1,6)
		TNT1 A 0 A_FireCustomMissile(WeaponFlash,0,0)
		TNT1 A 0 A_SetAngle(Angle+FRandom(-0.75,0.75))
		TNT1 A 1 A_SetPitch(Pitch+FRandom(-0.75,0.75))
		Stop
	Flash3:
		TNT1 A 0 A_FireCustomMissile("CG_Muzflash", Random(1, -1), 0, 8, 0, 0, Random(1, -1))
		TNT1 A 0 A_JumpIfInventory(PowerSilencer,1,4)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("Weapons/MachineGunFire",CHAN_WEAPON)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_PlaySound("Weapons/MachineGunFire",CHAN_WEAPON,0.5,0,3)
		TNT1 A 0 A_JumpIfInventory(Q2Bullets,1,3)
		TNT1 A 0 A_PlayWeaponSound("Weapons/NoAmmo")
		TNT1 A 0 A_Jump(256,3)
		TNT1 A 0 A_FireBullets(5,5,-1,6)
		TNT1 A 0 A_FireCustomMissile(WeaponFlash,0,0)
		TNT1 A 0 A_SetAngle(Angle+FRandom(-0.5,0.5))
		TNT1 A 1 A_SetPitch(Pitch+FRandom(-0.5,0.5))
		Stop
	}
}

ACTOR ChaingunReplacer : RandomSpawner Replaces Chaingun
{
	DropItem "Q2Machinegun" 256 100
	DropItem "Q2Chaingun" 256 33
}

ACTOR Q2HandGrenade : Q2Weapon
{
	Tag "Hand Grenade"
	Weapon.SelectionOrder 850
	+AMMO_OPTIONAL
	Weapon.SlotPriority 1
	Weapon.SlotNumber 1
	Weapon.AmmoUse 1
	Weapon.AmmoType Q2GrenadeAmmo
	Inventory.Icon "W_HGREN"
	States
	{
	Raise:
		TNT1 A 0 A_TakeInventory(WeaponIndex)
		TNT1 A 0 A_GiveInventory(WeaponIndex,11)
		GRN1 EF 5
		GRN0 A 5
		Goto Ready+1
	Deselect:
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("GetCvarInfo",0)==True,"InstantDeselect")
		GRN0 A 5 A_TakeInventory(Q2HoldingGrenade)
		GRN1 FE 5
		Goto InstantDeselect
	Ready:
		TNT1 A 0 A_Jump(32,2)
		GRN0 A 1 A_WeaponReady
		Loop
		GRN0 "BCDEFGHIJKLMNOPQRSTUVWXYZ\[]" 5 A_WeaponReady
		GRN1 ABCD 5 A_WeaponReady
		Goto Ready+1
	Fire:
		GRN1 F 4
		GRN1 G 4 A_PlayWeaponSound("Weapons/HGrenadeActivate")
		GRN1 HIJK 4
		GRN1 L 0 A_FireCustomMissile(Q2HGrenade,0,1)
		TNT1 A 0 A_GiveInventory(Q2HoldingGrenade,1)
		GRN1 LMN 3
	Hold:
		GRN1 O 3
		GRN1 P 0 A_JumpIfInventory(Q2HoldingGrenade,1,2)
		GRN1 P 0 A_Jump(256,2)
		GRN1 P 0 A_ReFire
		Goto Throw
		GRN1 P 20 A_ClearReFire
		Goto Ready+1
	Throw:
		GRN1 P 3 ACS_NamedExecuteAlways("Q2TauntScript",0,3)
		GRN1 Q 0 A_PlayWeaponSound("Weapons/HGrenadeThrow")
		GRN1 Q 4 A_GiveInventory(Q2GrenadeThrow)
		GRN1 RS 4
		GRN0 A 0 A_TakeInventory(Q2GrenadeThrow)
		TNT1 A 0 A_TakeInventory(Q2HoldingGrenade)
		TNT1 A 0 A_JumpIfInventory(Q2GrenadeAmmo,1,"Ready")
		TNT1 A 0 A_TakeInventory(Q2HandGrenade)
		Goto Ready
	}
}

ACTOR Q2GrenadeThrow : InvBoolean {}
ACTOR Q2HoldingGrenade : Q2GrenadeThrow {}

ACTOR Q2HGrenade
{
	Projectile
	-NOGRAVITY
	+FORCERADIUSDMG
	+MOVEWITHSECTOR
	+NOINTERACTION
	Speed 18
	Gravity 0.66
	Damage 25
	Radius 5
	Height 10
	BounceType Doom
	BounceSound "Weapons/HGrenadeBounce"
	Var Int User_Angle;
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_PlaySound("Weapons/HGrenadeCount",CHAN_BODY,0.75,3)
		TNT1 A 0 ACS_NamedExecuteAlways("Q2GrenadeTimer")
	Hide:
		TNT1 A 0 A_Warp(AAPTR_TARGET,-8,-8,42,180,WARPF_NOCHECKPOSITION|WARPF_STOP)
		TNT1 A 0 A_JumpIfInTargetInventory(Q2HoldingGrenade,1,1)
		Goto UnHide
		TNT1 A 1 A_JumpIfInTargetInventory(Q2GrenadeThrow,1,1)
		Loop
		TNT1 A 0 A_GiveInventory(Shell,1)
		TNT1 A 1 A_GiveInventory(Clip,1)
		Loop
	UnHide:
		TNT1 A 0 A_Turn(180)
		TNT1 A 0 A_ChangeFlag(NOINTERACTION,0)
		TNT1 A 0 A_Jump(128,2)
		TNT1 A 0 A_SetUserVar(User_Angle,Random(1,6))
		Goto Idle
		TNT1 A 0 A_SetUserVar(User_Angle,Random(-6,-1))
	Idle:
		TNT1 AAA 0 A_SpawnItemEx(Q2GrenadeSmoke,Random2(5),0,Random2(5),0,0,FRandom(0.1,0.5),Random(1,360),0,128)
		TNT1 A 0 A_SetAngle(Angle+User_Angle)
		SGRN A 1 A_SetPitch(Pitch-15)
		Loop
	Death:
		TNT1 A 0 A_PlaySound("Weapons/HGrenadeBounce",CHAN_VOICE)
		SGRN A 1 A_SpawnItemEx(Q2GrenadeSmoke,Random2(5),0,Random2(5),0,0,FRandom(0.1,1),Random(1,360),0,128)
		Wait
	XDeath:
		TNT1 A 0 A_JumpIfInventory(Shell,1,2)
		TNT1 A 0 A_TakeFromTarget(Q2HoldingGrenade)
		TNT1 A 0 A_PlaySound("Weapons/GrenadeExplosion")
		TNT1 A 0 A_SpawnItem(Q2Explosion)
		TNT1 A 1 A_Explode
		Stop
	}
}

ACTOR Q2GrenadeLauncher : Q2Weapon
{
	Tag "Grenade Launcher"
	Inventory.PickupMessage "Grenade Launcher"
	Weapon.SelectionOrder 500
	Weapon.AmmoGive 5
	Weapon.AmmoType Q2GrenadeAmmo
	Weapon.AmmoUse 1
	Weapon.SlotNumber 6
	Inventory.Icon "W_GLNCHR"
	States
	{
	Spawn:
		RLAU A -1
		Stop
	Raise:
		TNT1 A 0 A_TakeInventory(WeaponIndex)
		TNT1 A 0 A_GiveInventory(WeaponIndex,6)
		GRL0 ABCDEF 4
		Goto Ready+1
	Deselect:
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("GetCvarInfo",0)==True,"InstantDeselect")
		GRL1 A 2
		GRL0 STUVW 3
		Goto InstantDeselect
	Ready:
		TNT1 A 0 A_Jump(32,2)
		GRL1 A 1 A_WeaponReady
		Loop
		GRL1 "BCDEFGHIJKLMNOPQRSTUVWXYZ[^]" 5 A_WeaponReady
		GRL2 ABCDEFGHIJKLMN 5 A_WeaponReady
		Goto Ready+1
	Fire:
		TNT1 A 0 A_JumpIfInventory(PowerSilencer,1,4)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("Weapons/GrenadeLauncherFire",CHAN_WEAPON)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_PlaySound("Weapons/GrenadeLauncherFire",CHAN_WEAPON,0.5,0,3)
		TNT1 A 0 A_FireCustomMissile(Q2Grenade,0,1,9,0,0,10)
		TNT1 A 0 A_GunFlash
		GRL0 HI 3
		GRL0 J 0 A_JumpIfInventory(PowerSilencer,1,4)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("Weapons/GrenadeLauncherReload",0)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_PlaySound("Weapons/GrenadeLauncherReload",0,0.5,0,3)
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("GetCvarInfo",0)==True,"DFire")
		GRL0 JKLMNOPQR 4
		GRL1 A 2
		Goto Ready
	DFire:
		GRL0 JKLM 4
		GRL0 NOPQR 4 A_WeaponReady
		Goto Ready
	Flash:
		TNT1 A 1 A_Light2
		TNT1 A 0 A_SetPitch(Pitch-5.0)
		TNT1 AAAAA 1 A_SetPitch(Pitch+0.5)
		TNT1 A 0 A_Light1
		TNT1 AAAAA 1 A_SetPitch(Pitch+0.5)
		Goto LightDone
	}
}

ACTOR Q2Grenade
{
	Projectile
	-NOGRAVITY
	+FORCERADIUSDMG
	+MOVEWITHSECTOR
	Speed 25
	Gravity 0.66
	Damage 20
	Radius 5
	Height 10
	BounceType Doom
	BounceSound "Weapons/GrenadeBounce"
	Var Int User_Angle;
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_Jump(128,2)
		TNT1 A 0 A_SetUserVar(User_Angle,Random(1,6))
		Goto Idle
		TNT1 A 0 A_SetUserVar(User_Angle,Random(-6,-1))
	Idle:
		TNT1 AAA 0 A_SpawnItemEx(Q2GrenadeSmoke,Random2(5),0,Random2(5),0,0,FRandom(0.1,0.5),Random(1,360),0,128)
		TNT1 A 0 A_SetAngle(Angle+User_Angle)
		SGRN A 1 A_SetPitch(Pitch-15)
		Loop
	Death:
		TNT1 A 0 A_PlaySound("Weapons/GrenadeBounce")
		SGRN AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx(Q2GrenadeSmoke,Random2(5),0,Random2(5),0,0,FRandom(0.1,1),Random(1,360),0,128)
		TNT1 A 0 A_Jump(256,"XDeath")
	XDeath:
		TNT1 A 0 A_PlaySound("Weapons/GrenadeExplosion")
		TNT1 A 0 A_SpawnItem(Q2Explosion)
		TNT1 A 5 A_Explode
		Stop
	}
}

ACTOR Q2GrenadeSmoke : Q2BulletPuffParticle
{
	Scale 1
	States
	{
	Fade:
		"----" A 1 A_FadeOut(0.025)
		Wait
	}
}

ACTOR Q2RocketLauncher : Q2Weapon
{
	Tag "Rocket Launcher"
	Inventory.PickupMessage "Rocket Launcher"
	Weapon.SelectionOrder 500
	Weapon.AmmoGive 5
	Weapon.AmmoType Q2Rockets
	Weapon.AmmoUse 1
	Weapon.SlotNumber 7
	Inventory.Icon "W_RLNCHR"
	States
	{
	Spawn:
		RLAU A -1
		Stop
	Raise:
		TNT1 A 0 A_TakeInventory(WeaponIndex)
		TNT1 A 0 A_GiveInventory(WeaponIndex,7)
		RCK0 ABCDE 4
		Goto Ready+1
	Deselect:
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("GetCvarInfo",0)==True,"InstantDeselect")
		RCK0 N 2
		RCK1 WXYZ 3
		Goto InstantDeselect
	Ready:
		TNT1 A 0 A_Jump(32,2)
		RCK0 N 1 A_WeaponReady
		Loop
		RCK0 "OPQRSTUVWXYZ[^]" 5 A_WeaponReady
		RCK1 ABCDEFGHIJKLMNOPQRSTUV 5 A_WeaponReady
		Goto Ready+1
	Fire:
		TNT1 A 0 A_JumpIfInventory(PowerSilencer,1,4)
		TNT1 A 0 A_AlertMonsters
		RCK0 N 2 A_PlaySound("Weapons/RocketLauncherFire",CHAN_WEAPON)
		RCK0 F 0 A_Jump(256,2)
		RCK0 N 2 A_PlaySound("Weapons/RocketLauncherFire",CHAN_WEAPON,0.5,0,3)
		RCK0 F 0 A_FireCustomMissile(Q2Rocket,0,1,4,3)
		RCK0 F 3 A_GunFlash
		RCK0 G 3
		RCK0 H 0 A_JumpIfInventory(PowerSilencer,1,4)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("Weapons/RocketLauncherLoad",0)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_PlaySound("Weapons/RocketLauncherLoad",0,0.5,0,3)
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("GetCvarInfo",0)==True,"DFire")
		RCK0 HIJ 3
		RCK0 KLM 5
		Goto Ready
	DFire:
		RCK0 HIJK 3
		RCK0 LM 5 A_WeaponReady
		Goto Ready
	Flash:
		TNT1 A 0 A_SetPitch(Pitch-5.0)
		TNT1 A 1 A_Light2
		TNT1 AAAAA 1 A_SetPitch(Pitch+0.5)
		TNT1 A 0 A_Light1
		TNT1 AAAAA 1 A_SetPitch(Pitch+0.5)
		Goto LightDone
	}
}

ACTOR Q2Rocket : Rocket Replaces Rocket
{
	Speed 20
	Damage 25
	-ROCKETTRAIL
	+BRIGHT
	DeathSound "Weapons/RocketExplosion"
	Decal Scorch
	States
	{
	Spawn:
		MIS1 AAAAA 0 A_SpawnItemEx(Q2GrenadeSmoke,Random2(5),0,Random2(5),0,0,FRandom(0.1,0.5),Random(1,360),0,128)
		MIS1 A 0 A_SpawnItemEx(Q2RocketTrail,0,0,0,FRandom(0,0.5),0,FRandom(-0.5,0),Random(1,360),128)
		MIS1 A 1 Light(ROCKET) A_PlaySound("Weapons/RocketFly",CHAN_VOICE,0.66,1)
		Loop
	Death:
		TNT1 A 0 A_Explode
		TNT1 A 5 A_SpawnItem(Q2Explosion)
		Stop
	}
}

ACTOR Q2RocketTrail : Q2GrenadeSmoke { Translation "80:111=160:167" }

ACTOR RocketLauncherReplacer : RandomSpawner Replaces RocketLauncher
{
	DropItem "Q2GrenadeLauncher"
	DropItem "Q2RocketLauncher"
}

ACTOR Q2HyperBlaster : Q2Weapon
{
	Tag "HyperBlaster"
	Inventory.PickupMessage "Hyper Blaster"
	Weapon.SelectionOrder 50
	Weapon.AmmoGive 20
	Weapon.AmmoType Q2Cell
	Weapon.AmmoUse 1
	Weapon.SlotNumber 8
	Inventory.Icon "W_HYPERB"
	States
	{
	Spawn:
		PLAS A -1
		Stop
	Raise:
		TNT1 A 0 A_TakeInventory(WeaponIndex)
		TNT1 A 0 A_GiveInventory(WeaponIndex,8)
		HYP0 ABCDEF 3
		Goto Ready+1
	Deselect:
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("GetCvarInfo",0)==True,"InstantDeselect")
		TNT1 A 0 A_StopSound(CHAN_5)
		HYP2 A 2
		HYP0 GHIJ 3
		Goto InstantDeselect
	Ready:
		TNT1 A 0 A_Jump(32,2)
		HYP2 A 1 A_WeaponReady
		Loop
		HYP2 "ABCDEFGHIJKLMNOPQRSTUVWXYZ[^]" 5 A_WeaponReady
		Goto Ready+1
	Fire:
		HYP1 A 0 A_JumpIfInventory(PowerSilencer,1,4)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("Weapons/HyperBlasterLoop",CHAN_5,0.75)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_PlaySound("Weapons/HyperBlasterLoop",CHAN_5,0.33,0,3)
		TNT1 A 0 A_ReFire(2) TNT1 A 0 A_Jump(256,3)
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_FireCustomMissile(HyperBlasterProjectile,0,1,6,5)
		HYP1 A 4
		HYP1 B 0 A_ReFire(2) TNT1 A 0 A_Jump(256,3)
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_FireCustomMissile(HyperBlasterProjectile,0,1,6,2.5)
		HYP1 B 3
		HYP1 C 0 A_ReFire(2) TNT1 A 0 A_Jump(256,3)
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_FireCustomMissile(HyperBlasterProjectile,0,1,6,0)
		HYP1 C 4
		HYP1 D 0 A_ReFire(2) TNT1 A 0 A_Jump(256,3)
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_FireCustomMissile(HyperBlasterProjectile,0,1,6,-2.5)
		HYP1 D 3
		HYP1 E 0 A_ReFire(2) TNT1 A 0 A_Jump(256,3)
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_FireCustomMissile(HyperBlasterProjectile,0,1,6,0)
		HYP1 E 4
		HYP1 F 0 A_ReFire(2) TNT1 A 0 A_Jump(256,3)
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_FireCustomMissile(HyperBlasterProjectile,0,1,6,2.5)
		HYP1 F 3
		HYP1 A 0 A_ReFire
		//HYP1 ABCDEF 3 A_ReFire
	FireStop:
		HYP1 G 0 A_JumpIfInventory(PowerSilencer,1,4)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("Weapons/HyperBlasterStop",CHAN_5)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_PlaySound("Weapons/HyperBlasterStop",CHAN_5,0.5,0,3)
		HYP1 GHIJKLMNO 4
		Goto Ready
	Flash:
		TNT1 A 0 A_JumpIfInventory(PowerSilencer,1,4)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("Weapons/HyperBlasterFire",CHAN_WEAPON)
		TNT1 A 0 A_Jump(256,"Null")
		TNT1 A 0 A_PlaySound("Weapons/HyperBlasterFire",CHAN_WEAPON,0.5,0,3)
		Stop
	}
}

ACTOR HyperBlasterProjectile : BlasterProjectile
{
	Speed 40
	Damage 5
	MissileType ""
}

ACTOR Q2Railgun : Q2Weapon
{
	Tag "Railgun"
	Inventory.PickupMessage "Railgun"
	Weapon.SelectionOrder 300
	Weapon.AmmoGive 5
	Weapon.AmmoType Q2Slugs
	Weapon.AmmoUse 1
	Weapon.SlotNumber 9
	Decal RailScorch
	Inventory.Icon "W_RAILG"
	States
	{
	Spawn:
		PLAS A -1
		Stop
	Raise:
		TNT1 A 0 A_TakeInventory(WeaponIndex)
		TNT1 A 0 A_GiveInventory(WeaponIndex,9)
		TNT1 A 0 A_PlaySound("Weapons/RailGunLoop",CHAN_5,0.5,1)
		RLG0 ABCD 4
		Goto Ready+1
	Deselect:
		TNT1 A 0 A_StopSound(CHAN_5)
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("GetCvarInfo",0)==True,"InstantDeselect")
		RLG0 TUVWX 3
		Goto InstantDeselect
	Ready:
		TNT1 A 0 A_Jump(32,2)
		RLG1 A 1 A_WeaponReady
		Loop
		RLG1 "ABCDEFGHIJKLMNOPQRSTUVWXYZ[^]" 5 A_WeaponReady
		RLG2 ABCDEFGHI 5 A_WeaponReady
		Goto Ready+1
	Fire:
		TNT1 A 0 A_JumpIfInventory(PowerSilencer,1,4)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_RailAttack(Random(100,200),5,1,"2F 67 7F","White",RGF_FULLBRIGHT,1.0,"Q2RailgunPuff")
		TNT1 A 0 A_Jump(256,3)
		TNT1 A 0 A_PlaySound("Weapons/RailGF",CHAN_WEAPON,0.5,0,3)
		TNT1 A 0 A_RailAttack(Random(100,200),5,1,"2F 67 7F","White",RGF_FULLBRIGHT|RGF_SILENT,1.0,"Q2RailgunPuff")
		RLG0 E 3 A_GunFlash
		RLG0 FG 4
		RLG0 H 4
		RLG0 IJKLMNOPQRS 4
		Goto Ready
	Flash:
		TNT1 A 0 A_SetPitch(Pitch-5.0)
		TNT1 A 1 A_Light2
		TNT1 AAAAA 1 A_SetPitch(Pitch+0.5)
		TNT1 A 0 A_Light1
		TNT1 AAAAA 1 A_SetPitch(Pitch+0.5)
		Goto LightDone
	}
}

ACTOR Q2RailgunPuff : Q2BulletPuff { DamageType "Railgun" }

ACTOR PlasmaRifleReplacer : RandomSpawner Replaces PlasmaRifle
{
	DropItem "Q2HyperBlaster" 256 100
	DropItem "Q2Railgun" 256 50
}

//BFG10K, Original decorate taken from AEoD4 - Brightmaps and new decorate by TheCamaleonMaligno
ACTOR Q2BFG10K : Q2Weapon Replaces BFG9000
{
	Tag "BFG10K"
	Inventory.PickupMessage "You got the BFG10K!"
	Weapon.SelectionOrder 800
	Weapon.AmmoType Q2Cell
	Weapon.AmmoGive 50
	Weapon.AmmoUse 25
	+WEAPON.BFG
	Obituary "%o was disintegrated by %k's BFG10K."
	Weapon.SlotNumber 0
	Inventory.Icon "W_BFG"
	States
	{
	Spawn:
		BFUG A -1
		Stop
	Raise:
		TNT1 A 0 A_TakeInventory(WeaponIndex)
		TNT1 A 0 A_GiveInventory(WeaponIndex,10)
		TNT1 A 0 A_PlaySound("Weapons/BFG10KLoop",CHAN_5,1,1)
		QBFG A 3
		QBFG B 4
		QBFG C 3
		QBFG D 4
		QBFG E 3
		QBFG F 4
		Goto Ready+1
	Ready:
		TNT1 A 0
		QBFG G 1 A_WeaponReady
		QBFG G 0 A_Jump(10,1)
		Loop
		QBF2 DEFGHIJKLMNOPQRSTUVWXYZ 4 A_WeaponReady
		Goto Ready+1
	Deselect:
		TNT1 A 0 A_StopSound(CHAN_5)
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("GetCvarInfo",0)==True,"InstantDeselect")
		QBF3 ABCD 3
		TNT1 A 5
		Goto InstantDeselect
	Fire:
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("GetCvarInfo",0)==True,3)
		TNT1 A 0 A_JumpIfInventory(Q2Cell,50,2)
		QBFG G 20 A_PlayWeaponSound("Weapons/NoAmmo")
		Goto Ready
		TNT1 A 0 A_JumpIfInventory(PowerSilencer,1,4)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("Weapons/BFG10kFire",CHAN_WEAPON)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_PlaySound("Weapons/BFG10kFire",CHAN_WEAPON,0.5,0,3)
		QBFG HIJKLMNO 3
		TNT1 A 0 A_TakeInventory(Q2Cell,25,TIF_NOTAKEINFINITE)
		QBFG P 0 A_JumpIf(ACS_NamedExecuteWithResult("GetCvarInfo",0)==True,"DFire")
		QBFG P 0 A_FireCustomMissile("BFG10KBall",0,1,10,8)
		QBFG P 0 A_GunFlash
		QBFG PQQRSTUVWXYZ 3
		QBF2 ABC 3
		QBF2 C 0 A_CheckReload
		Goto Ready+1
	DFire:
		QBFG P 0 A_FireCustomMissile("BFG10KBall2",0,0,10,8)
		QBFG P 0 A_GunFlash
		QBFG PQQRSTUU 3
		QBFG VWXYZ 3 A_ReFire
		QBF2 ABC 3 A_ReFire
		QBF2 C 0 A_CheckReload
		Goto Ready+1
	Flash:
		TNT1 A 1 A_Light2
		TNT1 A 1 A_SetPitch(Pitch-15)
		TNT1 AAAA 2 A_SetPitch(Pitch+1.5)
		TNT1 A 0 A_Light1
		TNT1 AAAAAA 2 A_SetPitch(Pitch+1.5)
		Goto LightDone
	}
}

ACTOR BFG10KBall : BFGBall Replaces BFGBall
{
	DamageType BFG
	Damage 150
	Speed 10
	Decal BFGLightning
	-MISSILE
	+FORCEXYBILLBOARD
	+NOBLOCKMONST
	DeathSound "Weapons/BFG10KExplode"
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_SpawnItem(BFG10KOrbitParticleSpawner)
		TNT1 A 0 A_ChangeFlag(MISSILE,1)
		TNT1 A 0 A_JumpIf(VelX==0&&VelY==0,"Death")
		TNT1 A 0 A_PlaySound("Weapons/BFG10KFly",1,0.66,1)
		BFS1 ABAB 4 BRIGHT
		BFS1 AABB 2 BRIGHT A_SpawnItem(BFG10KBeamSpawner,0,0)
		Goto Spawn+8
	Death:
		BFE1 A 5 BRIGHT A_StopSound(1)
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx(BFG10KExplosionParticle,0,0,0,random(-4,4),random(-4,4),random(-4,4),random(1,360))
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx(BFG10KExplosionParticle,0,0,0,random(-4,4),random(-4,4),random(-4,4),random(1,360))
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx(BFG10KExplosionParticle,0,0,0,random(-4,4),random(-4,4),random(-4,4),random(1,360))
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx(BFG10KExplosionParticle,0,0,0,random(-4,4),random(-4,4),random(-4,4),random(1,360))
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx(BFG10KExplosionParticle,0,0,0,random(-4,4),random(-4,4),random(-4,4),random(1,360))
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx(BFG10KExplosionParticle,0,0,0,random(-4,4),random(-4,4),random(-4,4),random(1,360))
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx(BFG10KExplosionParticle,0,0,0,random(-4,4),random(-4,4),random(-4,4),random(1,360))
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx(BFG10KExplosionParticle,0,0,0,random(-4,4),random(-4,4),random(-4,4),random(1,360))
		BFE1 B 5 BRIGHT
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx(BFG10KExplosionParticle,0,0,0,random(-4,4),random(-4,4),random(-4,4),random(1,360))
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx(BFG10KExplosionParticle,0,0,0,random(-4,4),random(-4,4),random(-4,4),random(1,360))
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx(BFG10KExplosionParticle,0,0,0,random(-4,4),random(-4,4),random(-4,4),random(1,360))
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx(BFG10KExplosionParticle,0,0,0,random(-4,4),random(-4,4),random(-4,4),random(1,360))
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx(BFG10KExplosionParticle,0,0,0,random(-4,4),random(-4,4),random(-4,4),random(1,360))
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx(BFG10KExplosionParticle,0,0,0,random(-4,4),random(-4,4),random(-4,4),random(1,360))
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx(BFG10KExplosionParticle,0,0,0,random(-4,4),random(-4,4),random(-4,4),random(1,360))
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx(BFG10KExplosionParticle,0,0,0,random(-4,4),random(-4,4),random(-4,4),random(1,360))
		TNT1 A 0 A_Explode(64,192)
		TNT1 A 0 A_Explode(192,256,0)
		BFE1 C 5 BRIGHT A_BFGSpray("BFGExtra",40,18)
		TNT1 A 0 A_GiveInventory(Clip,1)// this thing removes the orbiting particles
		BFE1 DEF 5 BRIGHT
		Stop
	}
}

ACTOR BFG10KBall2 : BFG10KBall
{
	Damage 100
	Speed 25
	States
	{
	Death:
		BFE1 A 5 BRIGHT A_StopSound(1)
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx(BFG10KExplosionParticle,0,0,0,random(-4,4),random(-4,4),random(-4,4),random(1,360))
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx(BFG10KExplosionParticle,0,0,0,random(-4,4),random(-4,4),random(-4,4),random(1,360))
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx(BFG10KExplosionParticle,0,0,0,random(-4,4),random(-4,4),random(-4,4),random(1,360))
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx(BFG10KExplosionParticle,0,0,0,random(-4,4),random(-4,4),random(-4,4),random(1,360))
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx(BFG10KExplosionParticle,0,0,0,random(-4,4),random(-4,4),random(-4,4),random(1,360))
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx(BFG10KExplosionParticle,0,0,0,random(-4,4),random(-4,4),random(-4,4),random(1,360))
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx(BFG10KExplosionParticle,0,0,0,random(-4,4),random(-4,4),random(-4,4),random(1,360))
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx(BFG10KExplosionParticle,0,0,0,random(-4,4),random(-4,4),random(-4,4),random(1,360))
		BFE1 B 5 BRIGHT
		BFE1 C 5 BRIGHT A_BFGSpray
		TNT1 A 0 A_GiveInventory(Clip,1)
		BFE1 DEF 5 BRIGHT
		Stop
	}
}

ACTOR BFG10KExplosionParticle : Q2ExplosionParticle
{
	Translation "80:111=112:127"
	RenderStyle Add
	Scale 1.25
	States
	{
	Fade:
		"----" A 20 BRIGHT
		"----" A 5 BRIGHT
		"----" A 0 A_Jump(97,"Null")
		Goto Fade+1
	}
}

ACTOR BFG10KOrbitParticleSpawner : EffectBase
{
	States
	{
	Spawn:
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItem(BFG10KOrbitParticle)
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItem(BFG10KOrbitParticle)
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItem(BFG10KOrbitParticle)
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItem(BFG10KOrbitParticle)
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItem(BFG10KOrbitParticle)
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItem(BFG10KOrbitParticle)
	Idle:
		TNT1 A 1 A_JumpIfInTargetInventory(Clip,1,"Null")
		TNT1 A 0 A_Warp(AAPTR_TARGET,0,0,0,0,WARPF_NOCHECKPOSITION|WARPF_USECALLERANGLE,"Idle")
		Stop
	}
}

ACTOR BFG10KOrbitParticle : EffectBase
{
	Var Int User_Orbit[4];
	Radius 38
	Translation "80:111=112:127"
	RenderStyle Translucent
	Scale 1.15
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SetUserArray(User_Orbit,0,Random(20,Radius))
		TNT1 A 0 A_SetUserArray(User_Orbit,3,2)
		TNT1 A 0 A_SetUserArray(User_Orbit,1,FRandom(-User_Orbit[3]*65536,User_Orbit[3]*65536))
		TNT1 A 0 A_SetUserArray(User_Orbit,2,FRandom(-User_Orbit[3]*65536,User_Orbit[3]*65536))
		TNT1 A 0 A_SetTranslucent((Radius-(User_Orbit[0]-20))*0.1,1)
		TNT1 A 0 A_SetAngle(Random(1,360))
		TNT1 A 0 A_SetPitch(Random2(45))
	Decide:
		P001 ABCDEFGH 0 A_Jump(64,"Idle")
		Loop
	Idle:
		"----" A 0 A_SetPitch(Pitch+(User_Orbit[1]*0.0000153))
		"----" A 1 BRIGHT A_SetAngle(Angle+(User_Orbit[2]*0.0000153))
		"----" A 0 A_Warp(AAPTR_TARGET,Cos(Pitch)*User_Orbit[0],Cos(Pitch)*User_Orbit[0],Sin(Pitch)*User_Orbit[0],0,WARPF_USECALLERANGLE|WARPF_NOCHECKPOSITION,"Idle")
		Stop
	}
}

ACTOR BFG10KBeamSpawner
{
	Projectile
	Radius 1
	Height 1
	+SEEKERMISSILE
	MeleeRange 768
	MaxTargetRange 768
	States
	{
	Spawn:
		TNT1 AA 0 A_RearrangePointers(AAPTR_DEFAULT,AAPTR_Target)
		TNT1 A 0 A_ClearTarget
		TNT1 A 0 A_CopyFriendliness
		TNT1 AAAA 0 A_Chase
		Stop
/*	Missile:
		TNT1 A 0 A_CustomRailgun(Random(4,10),0,0,0,RGF_SILENT,1,0,"Q2BulletPuff",0,0,8129,0,4.5,0,"BFG10KBeamTrail",-4)
		Stop*/
	Melee:
		TNT1 A 0 A_CustomRailgun(Random(8,5),0,0,0,RGF_SILENT,1,0,"Q2BulletPuff",0,0,8129,0,4.5,0,"BFG10KBeamTrail",-4)
		Stop
	}
}

ACTOR BFG10KBeamSpawnerWeak : BFG10KBeamSpawner
{
	States
	{
	Missile:
		TNT1 A 0 A_CustomRailgun(Random(3,8),0,0,0,RGF_SILENT,1,0,"Q2BulletPuff",0,0,8129,0,4.5,0,"BFG10KBeamTrail",-4)
		Stop
	}
}

ACTOR BFG10KBeamTrail : EffectBase
{
	Translation "80:111=112:127"
	Alpha 0.33
	Scale 5
	Alpha 0.66
	States
	{
	Spawn:
		P001 F 3 BRIGHT
		Stop
	}
}

ACTOR Q1SNG : Q2Weapon
{
	Tag "Super Nailgun"
	+CHEATNOTWEAPON
	Inventory.PickupMessage "Super Nailgun."
	Weapon.SelectionOrder 200
	Weapon.AmmoGive 50
	Weapon.AmmoType Q2Bullets
	Weapon.AmmoUse 2
	Weapon.SlotNumber 5
	Inventory.Icon "W_NAIL2"
	States
	{
	Spawn:
		MGUN A -1
		Stop
	Select:
		TNT1 A 0 A_TakeInventory(WeaponIndex)
		TNT1 A 0 A_GiveInventory(WeaponIndex,19)
		SNG1 A 0 A_Raise
		SNG1 A 1 A_Raise
		Goto Select+2
	Deselect:
		SNG1 A 0 A_Lower
		SNG1 A 1 A_Lower
		Loop
	Ready:
		TNT1 A 0
		SNG1 A 1 A_WeaponReady
		Loop
	Fire:
		SNG1 B 3 A_GunFlash
		SNG1 C 0 A_ReFire(2) TNT1 A 0 A_Jump(256,"Ready")
		SNG1 C 4 A_GunFlash
		SNG1 D 0 A_ReFire(2) TNT1 A 0 A_Jump(256,"Ready")
		SNG1 D 3 A_GunFlash
		SNG1 E 0 A_ReFire(2) TNT1 A 0 A_Jump(256,"Ready")
		SNG1 E 4 A_GunFlash
		SNG1 F 0 A_ReFire(2) TNT1 A 0 A_Jump(256,"Ready")
		SNG1 F 3 A_GunFlash
		SNG1 G 0 A_ReFire(2) TNT1 A 0 A_Jump(256,"Ready")
		SNG1 G 4 A_GunFlash
		SNG1 H 0 A_ReFire(2) TNT1 A 0 A_Jump(256,"Ready")
		SNG1 H 3 A_GunFlash
		SNG1 I 0 A_ReFire(2) TNT1 A 0 A_Jump(256,"Ready")
		SNG1 I 4 A_GunFlash
		SNG1 B 0
		Goto Ready
	Flash:
		TNT1 A 0 A_Light1
		TNT1 A 0 A_FireCustomMissile(WeaponFlash,0,0)
		TNT1 A 0 A_FireCustomMissile(SNGNail)
		TNT1 A 0 A_PlayWeaponSound("Weapons/SNG")
		TNT1 A 1 A_SetPitch(Pitch-0.66)
		TNT1 AA 1 A_SetPitch(Pitch+0.33)
		Goto LightDone
	}
}

ACTOR SNGNail
{
	Projectile
	Speed 35
	Damage 4
	Radius 3
	Height 6
	+BLOODSPLATTER
	Decal BulletChip
	States
	{
	Spawn:
		PUFF A -1
		Stop
	Crash:
	Death:
		TNT1 A 1 A_SpawnItem(BulletPuff)
		Stop
	XDeath:
		TNT1 A 0
		Stop
	}
}

ACTOR Q1LightningGun : Q2Weapon
{
	Tag "Thunderbolt"
	+CHEATNOTWEAPON
	Weapon.SelectionOrder 150
	Inventory.PickupMessage "Thunderbolt"
	Weapon.SlotNumber 8
	Weapon.AmmoType "Q2Cell"
	Weapon.AmmoGive 40
	Weapon.AmmoUse 1
	Inventory.Icon "W_LIGHT"
	Obituary "%o was electrocuted by %k's thunderbolt."
	States
	{
	Spawn:
		PLAS A -1
		Stop
	Select:
		TNT1 A 0 A_TakeInventory(WeaponIndex)
		TNT1 A 0 A_GiveInventory(WeaponIndex,20)
		THND A 0 A_Raise
		THND A 1 A_Raise
		Goto Select+2
	Deselect:
		THND A 0 A_Lower
		THND A 1 A_Lower
		Loop
	Ready:
		TNT1 A 0
		THND A 1 A_WeaponReady
		Loop
	Fire:
		THND A 0 A_PlayWeaponSound("Weapons/LightningStart")
		TNT1 A 0 A_TakeInventory(Q2Cell,1,TIF_NOTAKEINFINITE)
		THND BC 3 A_GunFlash
		TNT1 A 0 A_TakeInventory(Q2Cell,1,TIF_NOTAKEINFINITE)
		THND DE 3 A_GunFlash
		TNT1 A 0 A_ReFire
		goto Ready
	Hold:
		TNT1 A 0 A_PlaySound("Weapons/Lightning",CHAN_5)
		TNT1 A 0 A_TakeInventory(Q2Cell,1,TIF_NOTAKEINFINITE)
		THND BC 3 A_GunFlash
		THND D 0 A_TakeInventory(Q2Cell,1,TIF_NOTAKEINFINITE)
		THND DE 3 A_GunFlash
		TNT1 A 0 A_ReFire(1)
		goto Ready
		TNT1 A 0 A_TakeInventory(Q2Cell,1,TIF_NOTAKEINFINITE)
		THND BC 3 A_GunFlash
		TNT1 A 0 A_TakeInventory(Q2Cell,1,TIF_NOTAKEINFINITE)
		THND DE 3 A_GunFlash
		TNT1 A 0 A_ReFire
		goto Ready
	Flash:
		TNT1 A 0 A_Light2
		TNT1 A 0 A_FireCustomMissile(ThunderBoltLightning,0,0,0,-7)
		TNT1 A 1 A_SetPitch(Pitch-0.5)
		TNT1 A 1 A_SetPitch(Pitch+0.5)
		TNT1 A 0 A_Light0
		Stop
	}
}

ACTOR ThunderBoltLightning : FastProjectile
{
	Speed 22
	Damage 4
	DamageType Electric
	+STRIFEDAMAGE
	+NOTIMEFREEZE
	+BLOODSPLATTER
	Height 5
	Radius 3
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_ChangeFlag(PAINLESS,Random(0,128))
	Moving:
		TNT1 A 0 A_JumpIfCloser(5000,1)
		Stop
		TNT1 A 0 A_SpawnItemEx(ThunderBoltLightningTrail,0,0,0,(MomX*0.01), (MomY*0.01), (MomZ*0.01), Angle, 128 | SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEMOMENTUM | SXF_SETMASTER | SXF_ABSOLUTEANGLE)
		
		TNT1 A 0 A_Warp(AAPTR_DEFAULT,MomX/5,MomY/5,MomZ/5,0,WARPF_ABSOLUTEOFFSET,1) Goto Death
		TNT1 A 0 A_Warp(AAPTR_DEFAULT,MomX/5,MomY/5,MomZ/5,0,WARPF_ABSOLUTEOFFSET,1) Goto Death
		TNT1 A 0 A_Warp(AAPTR_DEFAULT,MomX/5,MomY/5,MomZ/5,0,WARPF_ABSOLUTEOFFSET,1) Goto Death
		TNT1 A 0 A_Warp(AAPTR_DEFAULT,MomX/5,MomY/5,MomZ/5,0,WARPF_ABSOLUTEOFFSET,1) Goto Death
		TNT1 A 0 A_Warp(AAPTR_DEFAULT,MomX/5,MomY/5,MomZ/5,0,WARPF_ABSOLUTEOFFSET,"Moving")
		Stop
	Death:
		TNT1 A 1 Light(PLASMA_X3) A_SetTranslucent(1,1)
		SPK1 C 2 BRIGHT Light(PLASMA_X3) A_SetScale(FRandom(0.33,1))
		TNT1 A 3 Light(PLASMA_X3)
		TNT1 A 5 Light(PLASMA_X4)
		Stop
	}
}

ACTOR ThunderBoltLightningTrail : EffectBase
{
	+NOTIMEFREEZE
	RenderStyle Normal
	Scale 0.5
	States
	{
	Spawn:
		PLSE BCDE 0 A_Jump(64,"Fade")
		Loop
	Fade:
		"####" "#" 3 BRIGHT
		Stop
	}
}